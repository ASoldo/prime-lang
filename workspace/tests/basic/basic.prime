test tests::basic;

import pkg::lib::{banner, promote, describe};
import core::types;

fn adds_numbers() -> (int32, int32) {
  (1, 4)
}

fn adds_two_numbers(a: int32, b: int32) -> bool {
  a + b == 5
}

fn validates_imported_identity() -> bool {
  let Banner b = promote("task", true);
  let string status = describe(b);
  status == "highlighted"
}

fn uses_asserts() -> bool {
  assert(true);
  expect(true, "expect should not panic");
  let string sentence = `one two three`;
  let int32 chars = sentence.str_len();
  let bool has_two = sentence.str_contains("two");
  let string trimmed = ` padded `.str_trim();
  let []string parts = sentence.str_split(" ");
  return chars == 13 && has_two && trimmed == "padded" && parts.len() == 3;
}

fn pointer_writes() -> bool {
  let mut int32 hp = 10;
  let ptr = ptr_mut(&mut hp);
  *ptr = *ptr + 5;
  hp == 15 && *ptr == 15
}

fn pointer_reads() -> bool {
  let int32 hp = 7;
  let ptr = ptr(&hp);
  *ptr == 7
}

fn range_is_storable() -> bool {
  let Range[int32] span = 1..=3;
  span;
  true
}

fn iterates_stored_range() -> bool {
  let Range[int32] span = 1..3;
  let mut int32 total = 0;
  for value in span {
    total = total + value;
  }
  total == 3
}

struct Counter {
  data: []int32;
}

impl Iterable[int32] for Counter {
  fn iter(counter: Counter) -> []int32 {
    counter.data
  }
}

fn iterates_custom_iterable() -> bool {
  let Counter counter = Counter{
    data: [4, 5],
  };
  let mut int32 total = 0;
  for value in counter {
    total = total + value;
  }
  total == 9
}

fn indexes_slice() -> bool {
  let []int32 values = [
    10,
    20,
  ];
  match values[1] {
    Some(found) => found == 20,
    None => false,
  }
}

fn indexes_map_literal() -> bool {
  let Map[string, int32] rewards = #{
    "Alpha": 3,
  };
  match rewards["Alpha"] {
    Some(found) => found == 3,
    None => false,
  }
}

fn index_bounds_are_safe() -> bool {
  let []string empty = [];
  match empty[0] {
    Some(_) => false,
    None => true,
  }
}

fn index_assignment_updates_slice() -> bool {
  let []int32 numbers = [
    1,
    2,
    3,
  ];
  numbers[1] = 42;
  match numbers[1] {
    Some(found) => found == 42,
    None => false,
  }
}

fn index_assignment_inserts_map() -> bool {
  let Map[string, int32] counts = #{
    "hp": 5,
  };
  counts["mp"] = 7;
  match counts["mp"] {
    Some(found) => found == 7,
    None => false,
  }
}
